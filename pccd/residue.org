* Residue

cite Henderson 2014

i,j,k,l: occupied
a,b,c,d: virtual
p,q,r,s: general

pCCD energy:
\begin{align*}
E= <0|H|0> + \sum_{ia} t_i^a v_{ii}^{aa}
\end{align*}

\begin{align*}
r_i^a &= v_{ii}^{aa} + 2(fcc_a^a - fcc_i^i - \sum_{j} t_j^a v_{aa}^{jj}
- \sum_{b} t_i^b v_{bb}^{ii})t_i^a \\
&-2(2 v_{ia}^{ia} - v_{ai}^{ia} - v_{aa}^{ii} t_i^a) t_i^a \\
&+ \sum_{b} t_i^b v_{bb}^{aa} +  \sum_{j} t_j^a v_{ii}^{jj}
+ \sum_{jb} v_{bb}^{jj} t_j^a t_i^b
\end{align*}
\begin{align*}
&= v_{ii}^{aa} + 2(fcc_a^a - fcc_i^i - U_{1_a} - U_{2_i}) t_i^a \\
&- 2(2v_{ia}^{ia} - v_{ai}^{ia} - v_{aa}^{ii} t_i^a) t_i^a \\
&+ U_{3_i}^a + U_{4_i}^a + U_{5_i}^a
\end{align*}

$$\sum_j t_j^a v_{aa}^{jj} = U_{1_a}$$
$$\sum_b t_i^b v_{bb}^{ii} = U_{2_i}$$
$$\sum_b t_i^b v_{bb}^{aa}=U_{3_i}^a$$
$$\sum_j t_j^a v_{ii}^{jj} = U_{4_i}^a$$
$$\sum_{b} v_{bb}^{jj} t_i^b = X_{i}^{j}$$
$$\sum_{jb} v_{bb}^{jj} t_j^a t_i^b=\sum_j X_i^j t_j^a = U_{5_i}^a$$
with $fcc_q^p$ an element of the Fock operator and $v_{rs}^{pq}$ is a
bi-electronic integral.

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine r2_pccd(nO, nV, t2, U1, U2, U3, U4, U5, r2)
  
  implicit none

  BEGIN_DOC
  ! pCCD residual equations
  END_DOC

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: U1(nV), U2(nO)
  double precision, intent(in)  :: U3(nO, nV)
  double precision, intent(in)  :: U4(nO, nV)
  double precision, intent(in)  :: U5(nO, nV)
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out) :: r2(nO, nV)

  ! internal
  integer                       :: i,a

  r2 = 0d0
  do a = 1, nV
     do i = 1, nO
        r2(i,a) = cc_space_v_iiaa(i,a) &
          + 2d0 *(cc_space_f_v(a) - cc_space_f_o(i) - U1(a) - U2(i)) * t2(i,a) &
          - 2d0*(2d0 * cc_space_v_iaia(i,a) &
          - cc_space_v_aiia(a,i) &
          - cc_space_v_aaii(a,i) * t2(i,a)) * t2(i,a) &
          + U3(i,a) + U4(i,a) + U5(i,a)
     enddo
   enddo
  
end  
#+END_SRC

* Intermediates
#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine U1_pccd(nO, nV, t2, U1)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out) :: U1(nV)

  ! internal
  integer                       :: j,a

  ! U1(a) = \sum_j t_j^a v_{aa}^{jj}
  U1 = 0d0
  do a = 1, nV
    do j = 1, nO
      U1(a) = U1(a) + t2(j,a) * cc_space_v_aaii(a,j)
    enddo
  enddo

  

end  
#+END_SRC

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine U2_pccd(nO, nV, t2, U2)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out) :: U2(nO)

  ! internal
  integer                       :: i,b

  ! U2(i) = \sum_b t_i^b v_{bb}^{ii} 
  U2 = 0d0
  do i = 1, nO
    do b = 1, nV
      U2(i) = U2(i) + t2(i,b) * cc_space_v_aaii(b,i)
    enddo
  enddo

end  
#+END_SRC

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine U3_pccd(nO, nV, t2, U3)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out) :: U3(nO, nV)

  ! internal
  integer                       :: i,a,b

  ! U3(i,a) = \sum_b t_i^b v_{bb}^{aa}
  !U3 = 0d0
  !do a = 1, nV
  !  do i = 1, nO
  !    do b = 1, nV
  !       U3(i,a) = U3(i,a) + t2(i,b) * cc_space_v_aabb(b,a)
  !    enddo
  !  enddo
  !enddo

  call dgemm('N','N', nO, nV, nV, &
             1d0, t2, size(t2,1), &
                  cc_space_v_aabb, size(cc_space_v_aabb,1), &
             0d0, U3, size(U3,1))
  
end  
#+END_SRC

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine U4_pccd(nO, nV, t2, U4)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out) :: U4(nO, nV)

  ! internal
  integer                       :: i,j,a

  ! U4(i,a) = \sum_j t_j^a v_{ii}^{jj}
  !         = \sum_j v_{ii}^{jj} t_j^a
  
  !U4 = 0d0
  !do a = 1, nV
  !  do i = 1, nO
  !    do j = 1, nO
  !      U4(i,a) = U4(i,a) + t2(j,a) * cc_space_v_iijj(i,j)
  !    enddo
  !  enddo
  !enddo

  call dgemm('N','N', nO, nV, nO, &
             1d0, cc_space_v_iijj, size(cc_space_v_iijj,1), &
                  t2, size(t2,1), &
             0d0, U4, size(U4,1))

end  
#+END_SRC

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine U5_pccd(nO, nV, t2, X, U5)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)
  double precision, intent(in)  :: X(nO, nO)

  ! out
  double precision, intent(out) :: U5(nO, nV)

  ! internal
  integer                       :: i,j,a

  ! U5(i,a) = \sum_{jb} t_j^a t_i^b v_{bb}^{jj}
  !         = \sum_j X(i,j) t_j^a
  !U5 = 0d0
  !do a = 1, nV
  !  do i = 1, nO
  !    do j = 1, nO
  !      U5(i,a) = U5(i,a) + t2(j,a) * X(i,j)
  !    enddo
  !  enddo
  !enddo

  call dgemm('N','N', nO, nV, nO, &
             1d0, X, size(X,1), t2, size(t2,1), 0d0, U5, size(U5,1))

end  
#+END_SRC

#+BEGIN_SRC f90 :comments org :tangle residue.irp.f
subroutine X_pccd(nO, nV, t2,X)
  
  implicit none

  ! in
  integer, intent(in)           :: nO, nV
  double precision, intent(in)  :: t2(nO, nV)

  ! out
  double precision, intent(out)  :: X(nO, nO)

  ! internal
  integer                       :: i,j,b

  ! X(i,j) = \sum_b t_i^b v_{bb}^{jj}
  !X = 0d0
  !do i = 1, nO
  !  do j = 1, nO
  !    do b = 1, nV
  !      X(i,j) = X(i,j) + t2(i,b) * cc_space_v_aaii(b,j)
  !    enddo
  !  enddo
  !enddo

  call dgemm('N','N', nO, nO, nV, &
             1d0, t2, size(t2,1), cc_space_v_aaii, size(cc_space_v_aaii,1), &
             0d0, X, size(X,1))

end  
#+END_SRC
